# GNU GENERAL PUBLIC LICENSE
# Author: Carlos Marcelo Cabrera <point_to_null@esdebian.org>, (C) 2010

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.

mplayer -quiet -cache 512 -cache-min 25\
    "$*" 2>&1 | awk -v q="'" '

BEGIN{
    FS = ": "
    IND = "  "
}

/^Cache empty/||/^Cannot seek/{
    print IND "Exiting... (Cache empty)" > "/dev/stderr"
    exit
}

{
    sub(/;UTC=.*$/, "")
    if (last == $0)
        next
    last = $0
}

/^Name/{
    uname = $2
}

/Genre/{
    ugenre = $2
}

/Artist:/{
    uartist = $2
}

/Title:/{
    utitle = $2
}

/^Demuxer/ && /Artist changed/{
    sub("Demuxer info Artist changed to ", "", $0)
    uartist = $0
}

/^Demuxer/ && /Title changed/{
    sub("Demuxer info Title changed to ", "", $0)
    utitle = $0
}

/^ICY/ && /StreamTitle/{
    sub("ICY Info: StreamTitle=", "", $0)
    print IND $0
    utitle = $0
    gsub(";", "", utitle)
    gsub(q, "", utitle)
}

/^Exiting.../||/^Capturing not enabled/{
    print IND $0 > "/dev/stderr"
}

utitle ~ "_"{
    gsub("_", " ", utitle)
}

utitle ~ " - "{
    n = split($2, l, " - ")
    uartist = l[n - 1]
    gsub(";", "", uartist)
    gsub(q, "", uartist)
    utitle = l[n]
}

uname{
    print IND " ===", uname, "==="
    uname = 0
}

ugenre{
    print IND "  ==", ugenre, "=="
    ugenre = 0
}

uartist{
    print IND "     " uartist ":"
    uartist = 0
}

utitle{
    print IND "       * " utitle
    utitle = 0
}

'
